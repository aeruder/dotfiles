" So much of this is stolen from rafi/config
if &compatible
  set nocompatible
endif

let g:vim_configdir=resolve(expand("<sfile>:p:h"))
let g:vim_maindir=resolve(fnamemodify(g:vim_configdir, ':h'))

execute "source" g:vim_configdir."/pathhelper.vim"

" Configure our vimrc_profile level
"  -1 means low version of vim (<= 7.3)
"  0 means we are on ssh or a slow machine (no autocomplete, etc.)
"  1 means everything
"  2 means we have admin-control and can install a bunch of helpers and stuff
"    (clang autocomplete, and so on).
if !exists('g:vimrc_profile')
  if has('nvim')
    let g:vimrc_profile = 2
  elseif v:version >= 740
    let g:vimrc_profile = 1
  else
    let g:vimrc_profile = -1
  endif
  if !has('nvim') && !has('python3')
    let g:vimrc_profile = -1
  " elseif len($SSH_CLIENT) && g:vimrc_profile > 0
  "   let g:vimrc_profile = 0
  endif
endif

function! s:dein_load_yaml(filename) abort
  let l:yaml = readfile(a:filename)
  let l:json = system("ruby ".fnameescape(PJN(PTN(g:vim_maindir), "yamldecode.rb")), l:yaml)
"  if v:shell_error != 0
"    let l:json = system("perl ".fnameescape(g:maindir).g:vim_sep."yamldecode.pl", l:yaml)
"  endif
"  if v:shell_error != 0
"    let l:json = system("python ".fnameescape(g:maindir).g:vim_sep."yamldecode.py", l:yaml)
"  endif
  let l:denite_plugins = json_decode(l:json)

  for plugin in l:denite_plugins
    call dein#add(plugin['repo'], extend(plugin, {}, 'keep'))
  endfor
  unlet l:denite_plugins
endfunction

function! s:source_file(path, ...) abort "{{{
  let use_global = get(a:000, 0, ! has('vim_starting'))
  let abspath = PJ(g:vim_configdir, a:path)
  if ! use_global
    execute 'source' fnameescape(abspath)
    return
  endif

  let content = map(readfile(abspath),
    \ "substitute(v:val, '^\\W*\\zsset\\ze\\W', 'setglobal', '')")
  let tempfile = tempname()
  try
    call writefile(content, tempfile)
    execute printf('source %s', fnameescape(tempfile))
  finally
    if filereadable(tempfile)
      call delete(tempfile)
    endif
  endtry
endfunction "}}}

" Set augroup
augroup MyAutoCmd
  autocmd!
  autocmd BufEnter * syntax sync fromstart
augroup END

" Initialize base requirements
if has('vim_starting')
  call s:source_file('init.vim')
  if has('nvim')
    call s:source_file('neovim.vim')
  endif
endif

" Initialize dein.vim (package manager)
let s:path = PJ(g:vim_cachedir, "dein")
let s:plugins_path = PJ(g:vim_configdir, "plugins.yaml")
if g:vimrc_profile >= 0 && dein#load_state(s:path)
  call dein#begin(s:path, [expand('<sfile>'), s:plugins_path])
  try
    call s:dein_load_yaml(s:plugins_path)
  catch /.*/
    echoerr v:exception
    echomsg 'Error loading config/plugins.yaml...'
    echomsg 'Caught: ' v:exception
    echoerr 'Please run: pip install --user PyYAML'
  endtry

  if isdirectory(PJ(g:vim_maindir, "dev"))
    call dein#local(PJ(g:vim_maindir, "dev"), {'frozen': 1, 'merged': 0})
  endif
  call dein#end()
  call dein#save_state()
  if dein#check_install()
    if ! has('nvim')
      set nomore
    endif
    call dein#install()
  endif
endif

" call s:source_file('plugins/all.vim')

filetype plugin indent on
syntax enable

if g:vimrc_profile >= 0 && has('vim_starting')
  call dein#call_hook('source')
  autocmd VimEnter * call dein#call_hook('post_source')
elseif g:vimrc_profile >= 0 && ! has('vim_starting')
  call dein#call_hook('source')
  call dein#call_hook('post_source')
endif

" Loading configuration modules {{{
call s:source_file('theme.vim')
call s:source_file('general.vim')
call s:source_file('indent.vim')
call s:source_file('terminal.vim')
call s:source_file('filetype.vim')
call s:source_file('mappings.vim')
call s:source_file('whitespace.vim')
call s:source_file('unused.vim')
call s:source_file('perldoc.vim')
call s:source_file('scratchfiles.vim')
" }}}

set secure
