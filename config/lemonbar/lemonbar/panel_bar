#!/usr/bin/env zsh
#
# Example panel for lemonbar

. "$PANEL_DIR"/panel_colors

num_mon=$(bspc query -M | wc -l)

while read -r line ; do
    case $line in
        C*)
            cpu="%{F$COLOR_SYS_FG}%{B$COLOR_SYS_BG}CPU: ${line#?}% %{B-}%{F-}"
            ;;
        U*)
            mem="%{F$COLOR_SYS_FG}%{B$COLOR_SYS_BG}MEM: ${line#?}% %{B-}%{F-}"
            ;;
        S*)
            # clock output
            sys="%{F$COLOR_SYS_FG}%{B$COLOR_SYS_BG} ${line#?} %{B-}%{F-}"
            ;;
        T*)
            # xtitle output
            title="%{F$COLOR_TITLE_FG}%{B$COLOR_TITLE_BG} ${line#?} %{B-}%{F-}"
            ;;
        V*)
            # volume
            volume="%{F$COLOR_SYS_FG}%{B$COLOR_SYS_BG}Volume: ${line#?} %{B-}%{F-}"
            ;;
        M*)
            music="%{F$COLOR_SYS_FG}%{B$COLOR_SYS_BG}Now Playing: ${line#?} %{B-}%{F-}"
            ;;
        W*)
            # bspwm's state
            typeset -A mons
            thismon=()
            thismonname=unknown
            line=${line#?}
            parts=("${(@s.:.)line}")
            while [ ${#parts} -gt 0 ] ; do
                item=${parts[1]}
                name=${item#?}
                case $item in
                    [mM]*)
                        if [ -n "$thismon" ]; then
                            mons["$thismonname"]="${(j::)thismon}"
                            thismon=()
                        fi
                        [ $num_mon -lt 2 ] && shift 1 parts && continue
                        case $item in
                            m*)
                                # monitor
                                FG=$COLOR_MONITOR_FG
                                BG=$COLOR_MONITOR_BG
                                ;;
                            M*)
                                # focused monitor
                                FG=$COLOR_FOCUSED_MONITOR_FG
                                BG=$COLOR_FOCUSED_MONITOR_BG
                                ;;
                        esac
                        thismonname="$name"
                        thismon+=("%{F${FG}}%{B${BG}}%{A:bspc monitor -f ${name}:} ${name} %{A}%{B-}%{F-}")
                        ;;
                    [fFoOuU]*)
                        case $item in
                            f*)
                                # free desktop
                                FG=$COLOR_FREE_FG
                                BG=$COLOR_FREE_BG
                                ;;
                            F*)
                                # focused free desktop
                                FG=$COLOR_FOCUSED_FREE_FG
                                BG=$COLOR_FOCUSED_FREE_BG
                                ;;
                            o*)
                                # occupied desktop
                                FG=$COLOR_OCCUPIED_FG
                                BG=$COLOR_OCCUPIED_BG
                                ;;
                            O*)
                                # focused occupied desktop
                                FG=$COLOR_FOCUSED_OCCUPIED_FG
                                BG=$COLOR_FOCUSED_OCCUPIED_BG
                                ;;
                            u*)
                                # urgent desktop
                                FG=$COLOR_URGENT_FG
                                BG=$COLOR_URGENT_BG
                                ;;
                            U*)
                                # focused urgent desktop
                                FG=$COLOR_FOCUSED_URGENT_FG
                                BG=$COLOR_FOCUSED_URGENT_BG
                                ;;
                        esac
                        thismon+=("%{F${FG}}%{B${BG}}%{A:bspc desktop -f ${name}:} ${name} %{A}%{B-}%{F-}")
                        ;;
                esac
                shift 1 parts
            done
            if [ -n "$thismon" ]; then
                mons["$thismonname"]="${(j::)thismon}"
                thismon=()
            fi
            temp=()
            for k in "${(@kn)mons[@]}"; do
                temp+=("${mons[$k]}")
            done
            wm="${(j::)temp}"
            ;;
    esac
    printf "%s\n" "%{l}${wm}%{c}${title}%{r}${music} ${cpu} ${mem} ${volume} ${sys}"
done
